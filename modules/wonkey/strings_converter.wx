Namespace wonkey.strings

#rem wonkeydoc Converts an unsigned long value to a string.

@param value Value to convert.

@param base Numeric base for conversion, eg: 2 for binary, 10 for decimal, 16 for hex etc.

@example
Print ULongToString(10,10) ' output: 10
Print ULongToString(10,16) ' output: A
Print ULongToString(10, 2) ' output: 1010
@end

#end
Function ULongToString:String( value:ULong,base:UInt )

	Local str:=""
	
	While value
		Local n:=value Mod base
		If n<10 str=String.FromChar( n+48 )+str Else str=String.FromChar( n+55 )+str
		value/=base
	Wend
	
	Return str ? str Else "0"

End

#rem wonkeydoc Converts a string to an unsigned long value.

@param str String to convert.

@param base Numeric base for conversion, eg: 2 for binary, 10 for decimal, 16 for hex etc.

@example
Print StringToULong("42",10)       ' output: 42
Print StringToULong("FA",16)       ' output: 250
Print StringToULong("10000000", 2) ' output: 128
@end

#end
Function StringToULong:ULong( str:String,base:UInt )

	Local value:ULong
	
	If base<=10
	
		For Local ch:=Eachin str
			If ch>=48 And ch<48+base value=value*base+(ch-48) Else Exit
		Next
		
		Return value
	Endif

	For Local ch:=Eachin str
		
		If ch>=48 And ch<58
			value=value*base+(ch-48)
		Else If ch>=65 And ch<65+(base-10)
			value=value*base+(ch-55)
		Else If ch>=97 And ch<97+(base-10)
			value=value*base+(ch-87)
		Else
			Exit
		Endif
	Next
	
	Return value
End

#rem wonkeydoc Converts a ulong value to a binary string.

@param value Value to convert.

@example
Print Bin(10)    ' output: 1010
Print Bin(255)   ' output: 11111111
Print Bin($F0FA) ' output: 1111000011111010
@end

#end
Function Bin:String( value:ULong )
	
	Return ULongToString( value,2 )
End

#rem wonkeydoc Converts a binary string to a ulong value.

@param str String to convert.

@example
Print ParseBin("1001")             ' output: 10
Print ParseBin("11111111")         ' output: 255
Print ParseBin("1111000011111010") ' output: 61690
@end

#end
Function ParseBin:ULong( str:String )
	
	Return StringToULong( str,2 )
End

#rem wonkeydoc Converts a ulong value to a hexadecimal string.

@param value Value to convert.

@example
Print Hex(10)    ' output: A
Print Hex(255)   ' output: FF
Print Hex($F0FA) ' output: F0FA
Print Hex(61690) ' output: F0FA
@end

#end
Function Hex:String( value:ULong )
	
	Return ULongToString( value,16 )
End

#rem wonkeydoc Converts a hexadecimal string to a ulong value.

@param str String to convert.

@example
Print ParseHex("A")            ' output: 10
Print ParseHex("FF")           ' output: 255
Print ParseHex("F0FA")         ' output: 61690
Print ParseHex("112233AABBCC") ' output: 18838593387468
@end

#end
Function ParseHex:ULong( str:String )
	
	Return StringToULong( str,16 )
End

#rem wonkeydoc Parse a boolean string.

Returns true if `str` equals "True", ignoring case. Otherwise, returns false.

@param str String to convert.

@example
Print ParseBool("True")     ' output: True
Print ParseBool("tRuE")     ' output: True
Print ParseBool("False")    ' output: False
Print ParseBool("F4lsE")    ' output: False
@end

#end
Function ParseBool:Bool( str:String )
	
	Return str.ToLower()="true"
End
