
Namespace wonkey.math

Using wonkey.types

'jl added
'------------------------------------------------------------
#rem wonkeydoc The value _Pi_ times 2.
#end
Const Pi2:Double = Pi * 2

#rem wonkeydoc The value _Pi_ times 4.
#end
Const Pi4:Double = Pi * 4

#rem wonkeydoc The value _Pi_ halved.
#end
const PiHalf:double = Pi * 0.5

#rem wonkeydoc The value _Pi_ / 180.
#end
const Pi180:double = Pi / 180

#rem wonkeydoc Converts degrees in angles. E.G. 90 degrees to radians
Degrees start from 0 pointing East and then go counterclockwise.
So:
90 degrees is up (north)
180 degrees is pointing left (west)
280 degrees is pointing down (south)
See Also [[Rad2Deg]]
#end
Function Deg2Rad:float( degrees:float )
	return degrees * Pi180' ) / 180.0
End function

#rem wonkeydoc Converts radians to degrees in angles.
Degrees start from 0 pointing East and then go counterclockwise.
So:
90 degrees is up (north)
180 degrees is pointing left (west)
280 degrees is pointing down (south)
See Also [[Deg2Rad]]
#end
Function Rad2Deg:float( radian:float )
	return radian / Pi180
End function

#rem wonkeydoc Liniear Interp
#end
function Lerp:Double( a:double, b:double, t:double )
	return (1.0-t) * a + t*b
End function

#rem wonkeydoc Linear Interp
#end
function Slerp:Double( a:double, b:double, t:double )
	return (1.0-t) * a + t*b
End function


' from std:


'Function  Lerp:float( fromValue:float, toValue:float, position:float )
'	Local value:float = toValue - fromValue
'	If value = 0 Then return fromValue
'	Return fromValue + (value * position)
'End
'
'Function  Lerp:double( fromValue:double, toValue:double, position:double )
'	Local value:double = toValue - fromValue
'	If value = 0 Then return fromValue
'	Return fromValue + (value * position)
'End

'very efficiant and order independant!!!
Function PointInTriangle:bool( px:float,  py:float, p0x:float, p0y:float, p1x:float, p1y:float, p2x:float, p2y:float )
	Local dX:float = px - p2x
	Local dY:float = py - p2y
	Local dX21:float = p2x - p1x
	Local dY12:float = p1y - p2y
	Local D:float = dY12 * (p0x - p2x) + dX21 * (p0y - p2y)
	Local s:float = dY12 * dX + dX21 * dY
	Local t:float = (p2y - p0y) * dX + (p0x - p2x) * dY
	
	If D < 0 Then
		Return s <= 0 And t <= 0 And s + t >= D
	End If
	
	Return s >= 0 And t >= 0 And s + t <= D
End

Function PointInQuad:bool( px:float,  py:float, p0x:float, p0y:float, p1x:float, p1y:float, p2x:float, p2y:float, p3x:float, p3y:float )
	Local tri1:bool = PointInTriangle( px, py,  p0x, p0y, p1x, p1y, p2x, p2y )
	If tri1 Then Return True
	Return PointInTriangle( px, py,  p2x, p2y, p3x, p3y, p0x, p0y )
End

Function Length:float( x:float,  y:float,  z:float,  x1:float,  y1:float, z1:float )
	x -= x1
	y -= y1
	z -= z1
	Return Sqrt( x*x + y*y + z*z )
End
	
Function Length:float( x:float,  y:float,  x1:float,  y1:float )
	x -= x1
	y -= y1
	Return Sqrt( x*x + y*y )
End

Function Length:float( x:int,  y:int,  z:int,  x1:int,  y1:int, z1:int )
	x -= x1
	y -= y1
	z -= z1
	Return Sqrt( x*x + y*y + z*z )
End
	
Function Length:float( x:int,  y:int,  x1:int,  y1:int )
	x -= x1
	y -= y1
	Return Sqrt( x*x + y*y )
End

Function Length:float( x:double,  y:double,  z:double,  x1:double,  y1:double, z1:double )
	x -= x1
	y -= y1
	z -= z1
	Return Sqrt( x*x + y*y + z*z )
End
	
Function Length:float( x:double,  y:double,  x1:double,  y1:double )
	x -= x1
	y -= y1
	Return Sqrt( x*x + y*y )
End

Function Length:float( x:int, y:int )
	Return Sqrt( x*x + y*y )
End

Function Length:float( x:float, y:float )
	Return Sqrt( x*x + y*y )
End

Function Length:Double( x:double, y:double )
	Return Sqrt( x*x + y*y )
End

Function Length:float( x:int, y:int, z:int )
	Return Sqrt( x*x + y*y + z*z )
End

Function Length:float( x:float, y:float, z:float )
	Return Sqrt( x*x + y*y + z*z )
End

Function Length:Double( x:double, y:double, z:double )
	Return Sqrt( x*x + y*y + z*z )
End

'------------------------------------------------------------

#rem wonkeydoc The value _Pi_.
#end
Const Pi:Double=3.1415926535897931

#rem wonkeydoc The value _Pi_ times 2.
#end
Const TwoPi:Double=6.2831853071795862

Extern

#rem wonkeydoc Computes the sine of an angle.

@param `x` the angle, in radians.

@return The sine of `x`.

#end
Function Sin:Double( x:Double )="std::sin"

#rem wonkeydoc Computes the cosine of an angle.

@param `x` the angle, in radians.

@return The cosine of `x`.

#end
Function Cos:Double( x:Double )="std::cos"

#rem wonkeydoc Computes the tangent of an angle.

@param `x` the angle, in radians.

@return The tangent of `x`.

#end
Function Tan:Double( x:Double )="std::tan"

#rem wonkeydoc Computes the inverse sine of a number.

@param `x` The number.

@return The inverse sine of `x`, in radians.

#end
Function ASin:Double( x:Double )="std::asin"

#rem wonkeydoc Computes the inverse cosine of a number.

@param `x` The number.

@return The inverse cosine of `x`, in radians.

#end
Function ACos:Double( x:Double )="std::acos"

#rem wonkeydoc Computes the inverse tagent of a number.

@param `x` The number.

@return The inverse tangent of `x`, in radians.

#end
Function ATan:Double( x:Double )="std::atan"

#rem wonkeydoc Computes the inverse tangent of a ratio.

Returns the inverse tangent of `y`/`x`, using the signs of the arguments to determine the quadrant of the result.

@param `y` The numerator.

@param `z` The denominator.

@return The inverse tangent of `y`/`x`, in radians.

#end
Function ATan2:Double( y:Double,x:Double )="std::atan2"

#rem wonkeydoc Computes the square root of a number.

@param `x` The number.

@return The square root of `x`.

#end
Function Sqrt:Double( x:Double )="std::sqrt"

#rem wonkeydoc Computes the floor of a number.

@param `x` The number.

@return The largest integral value not greater than `x`.

#end
Function Floor:Double( x:Double )="std::floor"

#rem wonkeydoc Computes the ceiling of a number.

@param `x` The number.

@return The smallest integral value not less than `x`.

#end
Function Ceil:Double( x:Double )="std::ceil"

#rem wonkeydoc Rounds a number to the nearest integral value.

@param `x` The number.

@return The integral value nearest to `x`.

#end
#if __TARGET__="android"
Public
Function Round:Double( x:Double )
	Return Floor( x+.5 )
End
Extern
#else
Function Round:Double( x:Double )="std::round"
#endif


#rem wonkeydoc Raises a number to a power.

@param `x` The number.

@return `x` raised to the power of `y`.

#end
Function Pow:Double( x:Double,y:Double )="std::pow"


#rem wonkeydoc Computes the natural logarithm of a number.

@param `x` The number.

@return The natural logarithm of `x`.

#end
Function Log:Double( x:Double )="std::log"

#rem wonkeydoc Computes the base 2 logarithm of a number.

@param `x` The number.

@return The base 2 logarithm of `x`.

#end
#if __TARGET__="android"
Public
Function Log2:Double( x:Double )
	Return Log(x)/Log(2)
End
Extern
#else
Function Log2:Double( x:Double )="std::log2"
#endif

#rem wonkeydoc Computes the base 10 logarithm of a number.

@param `x` The number.

@return The base 10 logarithm of `x`.

#end
Function Log10:Double( x:Double )="std::log10"

#rem wonkeydoc Raise _e_ to a power.

@param `x` The number.

@return The value _e_ raised to the power of `x`.

#end
Function Exp:Double( x:Double )="std::exp"


Public

#rem wonkeydoc Gets the smaller of two numbers.

@return The smaller of `x` and `y`.

#end
Function Min<T>:T( x:T,y:T )
	If x<=y Return x
	Return y
End

#rem wonkeydoc Gets the larger of two number.

@return The larger of `x` and `y`.

#end
Function Max<T>:T( x:T,y:T )
	If x>=y Return x
	Return y
End

#rem wonkeydoc Clamps a value to a range.

If `x` is less than `min`, `min` is returned.

If `x` is greater than `max`, `max` is returned.

Otherwise, `x` is returned.

@return `x` clamped to the range [`min`,`max`].

#end
Function Clamp<T>:T( value:T,min:T,max:T )
	If value<=min Return min
	If value>=max Return max
	Return value
End

'jl added
'------------------------------------------------------------
Function Clamp:float( value:float, min:float, max:float )
	If value <= min Return min
	If value >= max Return max
	
'	Print "b "+value+"   min "+min+"   max "+max
	Return value
End
'------------------------------------------------------------

#rem wonkeydoc Gets the absolute value of a number.

If `x` is less than 0, then `-x` is returned.

If `x` is greater than or equal to 0, then `x` is returned.

@return The absolute value of `x`.

#end
Function Abs<T>:T( x:T ) Where T Implements INumeric
	If x>=0 Return x
	Return -x
End

#rem wonkeydoc Gets the sign of a number.

Returns -1 is `x` less than 0, 1 if `x` is greater than 0 or 0 if `x` is equal to 0.

@return The sign of `x`.

#end
Function Sgn<T>:Int( x:T ) Where T Implements IIntegral
	If x<0 Return -1
	If x>0 Return 1
	Return 0
End

Function Sgn<T>:Double( x:T ) Where T Implements IReal
	If x<0 Return -1
	If x>0 Return 1
	Return 0
End

#rem wonkeydoc Scale a value from an initial range to a target range.

@param `start1` The initial start value.
@param `stop1` The initial stop value.
@param `start2` The target start value.
@param `stop2` The target stop value.

@return The input `value` scaled from (`start1`-`stop1`) to (`start2`-`stop2`).

#end
Function Scale:Double(value:Double, start1:Double, stop1:Double, start2:Double, stop2:Double) Where T Implements IReal
    Return start2 + (stop2 - start2) * ((value - start1) / (stop1 - start1))
End